* Usage

#+name: org-config
#+BEGIN_SRC emacs-lisp
  (defun org-query-is-active-project-p () 
    "Is the headline at point an active project"
    (and
     (not (org-query-is-parent-headline-p "^Someday / Maybe"))
     (org-query-if-project-p '("NEXT"))))

  (defun org-query-is-unstucking-action-p ()
    "Is the headline an action that makes it's project non-stuck."
    (org-query-if-todo-p '("NEXT" "WAITING")))

  (defun tasks-waiting ()
    `(tags-todo "/+WAITING|+DEFERRED"
                ((org-agenda-overriding-header "Tasks waiting for something")
                 (org-tags-match-list-sublevels nil)
                 (org-agenda-todo-ignore-scheduled t)
                 (org-agenda-todo-ignore-deadlines t)
                 )))

  (defun next-in-active ()
    `(tags-todo "/+NEXT"
                ((org-agenda-overriding-header "Next tasks in active projects")
                 (org-agenda-skip-function
                  (org-query-select "headline"
                              (and (org-query-if-ancestor-p 'org-query-is-active-project-p) 
                                   (org-query-if-project-task-p '("NEXT") '("NEXT")))))
                 (org-tags-match-list-sublevels t)
                 (org-agenda-todo-ignore-scheduled t)
                 (org-agenda-todo-ignore-deadlines t)
                 (org-agenda-todo-ignore-with-date t)
                 (org-agenda-sorting-strategy
                  '(todo-state-down effort-up category-keep)))))

  (defun backlog-of-active ()
    `(tags-todo "/+TODO"
                ((org-agenda-overriding-header "Backlog of active projects")
                 (org-agenda-skip-function
                  (org-query-select "headline" (org-query-if-ancestor-p 'org-query-is-active-project-p)))
                 (org-agenda-todo-ignore-scheduled t)
                 (org-agenda-todo-ignore-deadlines t)
                 (org-agenda-todo-ignore-with-date t)
                 (org-agenda-sorting-strategy
                  '(category-keep)))))

  (defun active-projects-without-next ()
    `(tags-todo "/+NEXT"
                ((org-agenda-overriding-header "Active projects without next task")
                 (org-agenda-skip-function
                  (org-query-select "tree"
                              (and (org-query-is-active-project-p)
                                   (not (org-query-if-child-p 'org-query-is-unstucking-action-p)))))
                 (org-tags-match-list-sublevels 't)
                 (org-agenda-sorting-strategy
                  '(category-keep)))))

  (defun active-projects-with-next ()
    `(tags-todo "/+NEXT"
                ((org-agenda-overriding-header "Active projects with a next task")
                 (org-agenda-skip-function
                  (org-query-select "tree"
                              (and (org-query-is-active-project-p)
                                   (org-query-if-child-p 'org-query-is-unstucking-action-p))))
                 (org-tags-match-list-sublevels 't)
                 (org-agenda-sorting-strategy
                  '(category-keep)))))

  (defun loose-tasks ()
    `(tags-todo "/+TODO"
                ((org-agenda-overriding-header "Tasks not belonging to a project")
                 (org-agenda-skip-function
                  (org-query-select "headline"
                              (not (or
                               (org-query-if-project-task-p)
                               (org-query-if-project-p)
                               (org-is-habit-p)
                               (org-query-is-parent-headline-p "\\(^Inbox\\|^Someday / Maybe\\)")))))
                 (org-agenda-todo-ignore-scheduled t)
                 (org-agenda-todo-ignore-deadlines t)
                 (org-agenda-todo-ignore-with-date t)
                 (org-agenda-sorting-strategy
                  '(category-keep)))))

  (defun checklists ()
    `(tags "CHECKLIST"
           ((org-agenda-overriding-header "Checklists")
            (org-tags-match-list-sublevels nil))))

  (defun inbox ()
    `(tags-todo "LEVEL=2"
                ((org-agenda-overriding-header "Tasks to refile")
                 (org-agenda-skip-function (org-query-select "tree" (org-query-is-parent-headline-p "Inbox")))
                 (org-tags-match-list-sublevels nil))))


  (setq org-agenda-custom-commands
        `((" " "Agenda"
           ((agenda "" ((org-agenda-ndays 1)))
            ,(inbox)
            ,(tasks-waiting)
            ,(next-in-active)
            ,(active-projects-with-next)
            ,(active-projects-without-next)
            ,(backlog-of-active)
            ,(checklists))
           nil)
          ("r" "Review Agenda"
           ((agenda "" ((org-agenda-ndays 1)))
            ,(inbox)
            ,(loose-tasks)
            ,(tasks-waiting)
            ,(next-in-active)
            ,(active-projects-with-next)
            ,(active-projects-without-next)
            ,(backlog-of-active)
            ,(checklists))
           nil)))
#+END_SRC
